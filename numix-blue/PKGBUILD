# Maintainer: Kaleb Klein <klein.jae@gmail.com>

# Based on numix-themes-git
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Diego <cdprincipe@gmail.com>

pkgname=numix-themes-blue
pkgver=2.6.6
pkgrel=1
pkgdesc='A flat and light theme with a modern look (GNOME, Openbox, Unity, Xfce) (Blue Variant)'
arch=('any')
url='http://numixproject.org/'
license=('GPL3')
makedepends=('git' 'glib2' 'ruby-sass')
optdepends=('gtk-engine-murrine: GTK2 theme engine')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/numixproject/numix-gtk-theme/archive/${pkgver}.tar.gz"
        'numix-blue.patch')
md5sums=('7533cdecb2e9f875cd7efc881b8d65a3'
         '29a91b22ae30c0f58040354254fa0ce0')

prepare() {
  cd "$srcdir/numix-gtk-theme-$pkgver/src/"

  patch -Np1 -i "$srcdir/numix-blue.patch"
  ln -srf gtk-3.0/gtk{-dark,}.css
  ln -srf gtk-3.20/gtk{-dark,}.css
}

build() {
  cd "$srcdir/numix-gtk-theme-$pkgver/"
  make
}

package() {
  cd "$srcdir/numix-gtk-theme-$pkgver/"
  make INSTALL_DIR="${pkgdir}/usr/share/themes/Numix-Blue" install
}

# vim: ts=2 sw=2 et:
